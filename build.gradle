
apply plugin: 'java-library-distribution'
apply plugin: 'eclipse'

group = 'org.reelos.spigot'
version = '1.0.0'

sourceCompatibility = 1.8	

repositories {
	mavenCentral()
	maven {
		url 'https://hub.spigotmc.org/nexus/content/groups/public/'
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
		url 'http://maven.elmakers.com/repository/'
	}
}

dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.8+'
	compile 'org.glassfish:javax.json:1.0.4'
	
	testCompile 'junit:junit:4.12'
}

jar {
	manifest {
		attributes 'Class-Path' : project.configurations.runtime
				.collect{ file -> "lib/${file.name}" }
				.sort{ it.toLowerCase() }
				.join(' ')
	}
}

tasks.withType(Tar) {
	compression = Compression.GZIP
}

eclipse {
	classpath {
		file.withXml {
			def spigotApi = it.asNode().children().find{ entry ->
				return entry['@path'].contains('spigot-api') 
			}
			Node attributes = new Node(spigotApi, 'attributes')
			Node attribute = new Node(attributes, 'attribute')
			attribute.'@name' = 'javadoc_location'
			attribute.'@value' = 'https://hub.spigotmc.org/javadocs/spigot/'
		}
	}
}
